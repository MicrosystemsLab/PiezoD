# Set up the 1D mesh
# Assumes a 10um device layer and 1um BOX
line x loc=0.0	             tag=left
line x loc=1.0	             tag=right
line y loc=0.0	spacing=0.01 tag=top
line y loc=5.0	spacing=0.01 tag=bot

region silicon xlo=left xhi=right ylo=top yhi=bot
initialize <100> impurity=${dopantBackground} i.resistivity=10
method vertical ddc.full pd.5str

# 1) Grow the 250A protection oxide
diffusion time=20 temperature=800             inert
diffusion time=20 temperature=800 t.final=900 inert
diffusion time=10 temperature=900             dryO2
diffusion time=9  temperature=900             wetO2
diffusion time=10 temperature=900             dryO2
diffusion time=20 temperature=900 t.final=800 inert

# List the layers
select       z=doping
print.1d     x.val=0 layers

# 2) Ion implant
implant ${dopantShort} dose=${dose} energy=${energy} + 
	tilt=7 impl.tab=tr.${dopantLong} damage

# Output the total dopant concentration (unactivated)
select z=${dopantLong}
print.1d out.file=simulationOutputs/${outputFileName}_preAnneal.out

# List the layers
select			z=doping
print.1d		x.val=0 layers

# 4) Oxidize (optional) and anneal
${oxideStripCmd}
diffusion time=45 temperature=800 t.final=${temp}      inert
diffusion time=${dryOxidationTime} temperature=${temp} dryO2
diffusion time=${wetOxidationTime} temperature=${temp} wetO2
diffusion time=${dryOxidationTime} temperature=${temp} dryO2
diffusion time=${time}             temperature=${temp} inert
diffusion time=45    temperature=${temp} t.final=800   inert

# Output the post-anneal profile
# Specify the maximum temperature for correct solid solubility handling
select z=abs(doping) temperat=${temp}
print.1d out.file=simulationOutputs/${outputFileName}.out

# List the layers
select z=doping temperat=${temp}
print.1d x.val=0 layers

# 6) Analysis (optional)
# Sheet resistance
electric resistance x.val=0

# Peak doping level
select z=abs(doping) temperat=${temp}
extract silicon x.val=0 maximum val.extr assign name=Np0

# Junction depth
select z=doping temperat=${temp}
extract silicon x.val=0 value=0 d.extrac assign name=Dj

# Total carriers
select z=abs(doping) temperat=${temp}
extract silicon p1.y=0 p2.y=@Dj integral assign name=Ncm2
